### 一个例子
```
public class MyException {
    public static void main(String[] args) {
        div(1,0);
    }

    public static double div(int a, int b) {
        try {
            double res = a / b;
            System.out.println("in try");
            return res;
        } catch (ArithmeticException e) {
            System.out.println("in catch");
            throw new RuntimeException("...");
        } finally {
            System.out.println("in finally");
        }
    }
}
```
### 反编译查看字节码
使用`javac MyException.java`编译源代码之后，使用`javap -c MyException.class`，输出如下
```
Compiled from "MyException.java"
public class MyException {
  public MyException();
    Code:
       0: aload_0
       1: invokespecial #1                  // Method java/lang/Object."<init>":()V
       4: return

  public static void main(java.lang.String[]);
    Code:
       0: iconst_1
       1: iconst_0
       2: invokestatic  #7                  // Method div:(II)D
       5: pop2
       6: return
  public static double div(int, int);
    Code:
       0: iload_0
       1: iload_1
       2: idiv
       3: i2d
       4: dstore_2
       5: getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
       8: ldc           #19                 // String in try
      10: invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      13: dload_2
      14: dstore        4
      16: getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
      19: ldc           #27                 // String in finally
      21: invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      24: dload         4
      26: dreturn
      27: astore_2
      28: getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
      31: ldc           #31                 // String in catch
      33: invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      36: new           #33                 // class java/lang/RuntimeException
      39: dup
      40: ldc           #35                 // String ...
      42: invokespecial #37                 // Method java/lang/RuntimeException."<init>":(Ljava/lang/String;)V
      45: athrow
      46: astore        6
      48: getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
      51: ldc           #27                 // String in finally
      53: invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      56: aload         6
      58: athrow
    Exception table:
       from    to  target type
           0    16    27   Class java/lang/ArithmeticException
           0    16    46   any
          27    48    46   any
}
```
### 分析
#### 为什么 finally 语句块可以兜底？

内联机制：编译之后，finally语句块会被插到try和catch语句块的最后，即`return`和`throw`语句之前。

阅读上述字节码，显而易见，finally语句块对应的字节码为
```
getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
ldc           #27                 // String in finally
invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
```
这份字节码出现了三次，第一次在`try`语句块的`return`语句之前，第二次在`catch`语句块的`throw`语句之前，第三次就是在代码行48。

所以，在`try/catch`语句块中，`finally`代码块总是可以在函数返回或者抛出异常前被执行。

> 金科玉律：不要在finally语句块中包含`return`和`throw`语句，这会使程序提前结束。

#### 为什么发生异常后程序可以跳到指定的代码块继续执行？
存在`try/catch`语句块的方法编译后会生成异常表，通过查异常表，程序可以调到指定的catch块执行。

以上述例子为例，编译之后生成的异常表为
```
    Exception table:
       from    to  target type
           0    16    27   Class java/lang/ArithmeticException
           0    16    46   any
          27    48    46   any
```
程序执行时，如果在代码行`0~16`（即try语句块）发生异常，且类型为`ArithmeticException`，那么跳程序跳到代码行`27`（即catch语句块）继续执行，否则跳到代码行`46`执行；如果在代码行`27~48`发生异常（即catch语句块），也是跳到代码行`46`。

代码行46开始的语句表示的意思是：执行finally语句块，并抛出异常。也就是说，当抛出了无法捕获的异常，或者处理异常时又抛出了异常，那么会在执行`finally`语句块逻辑后向上抛出异常，提前结束函数。
```
46: astore        6
48: getstatic     #13                 // Field java/lang/System.out:Ljava/io/PrintStream;
51: ldc           #27                 // String in finally
53: invokevirtual #21                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
56: aload         6
58: athrow
```
